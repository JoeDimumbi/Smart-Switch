;SW-SMART.asm
;***********************************************SMART SWITCH***************************************************************
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;FILOSOFIA DE OPERACION:;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;EL SENSOR PIR (GPIO 0) DETECTARA PRESENCIA EN LA HABITACIÓN, EN CASO QUE EXISTA, EL MICROCONTROLADOR VERIFICARA LA CANTIDAD
; DE LUZ EN EL CUARTO, CENSADA POR LA LDR (GPIO 1) EN CASO QUE EXISTA POCA LUZ MANDARA UN NIVEL ALTO Y ACTIVARA LA CARGA
;(ENCENDERÁ LA LÁMPARA) (GPIO 4) LA CUAL ESTARÁ CONTROLADA POR UN RELEVADOR. EN CASO DE QUE EXISTA SUFICIENTE ILUMINACIÓN
;EN LA HABITACIÓN NO ENCENDERÁ LA LÁMPARA Y, VOLVERÁ A VERIFICAR LOS SENSORES DE PRESENCIA (PIR) Y DE LUZ (LDR).
;SI SE DESEA MANIPULAR LA CARGA SIN IMPORTAR EL ESTADO DE LOS SENSORES, EXISTIRÁ UN SWITCH ÓPTICO (GPIO 3) SIENDO UN EMISOR
; Y RECEPTOR INFRARROJO; EL CUAL AL PASAR LA MANO POR ÉL, DEBERÁ MANDAR UN NIVEL ALTO, CHECARA SI LA LÁMPARA ESTÁ ENCENDIDA
; O APAGADA, EN CASO DE QUE  ÉSTE APAGADA LA ENCENDERÁ Y EN CASO CONTRARIO LA APAGARA; EN CASO DE HABERLA ACTIVADO LA CARGA
;ESPERARA 5 MIN PARA DESACTIVARSE AUTOMÁTICAMENTE, CUANDO TERMINE DICHO TIEMPO VERIFICARA  LOS SENSORES Y ACTUARA EN FUNCIÓN
;DE LO CENSADO.
;EL CICLADO PRINCIPAL, SERÁ VERIFICANDO EL ESTADO DEL SENSOR PIR Y EL SWITCH ÓPTICO, HASTA QUE ALGUNO DE LOS DOS CAMBIE
; DE ESTADO ACTIVARA LA CARGA.
;
;EL SENSOR DE LUZ (LDR) DEBE APUNTAR A LUGAR OPUESTO A LA LÁMPARA, ENFOCARLO DE PREFERENCIA A LA VENTANA, PARA EVITAR
;SWITCHEOS EN FALSO CONTARÁ CON UN POTENCIÓMETRO PARA AJUSTAR LA SENSIBILIDAD DE LA PERCEPCIÓN DE LUZ.
;
;MIENTRAS ESTE ACTIVADA LA CARGA, SE PUEDE DESACTIVAR EN EL MOMENTO QUE UNO LO DESEE PERO EN CASO QUE EXISTA PRESENCIA
;NO PODRÁ DESACTIVARLA HASTA QUE CAMBIE ÉSTE ESTADO.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
        LIST p = 12F508
        #INCLUDE "p12F508.inc"

; CONFIG
; __config 0xFFEE
 __CONFIG _OSC_IntRC & _WDT_OFF & _CP_OFF & _MCLRE_OFF

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    CBLOCK 0X08
    ENDC
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;CONFIGURO DE ESTA FOMA LAS ENTRADAS Y SALIDAS, PENSANDO A FUTURO GPIO4 Y 5 COMO COMUNICACION CON OTROS DISPOSITIVOS
;POR MEDIO DE COMUNICACION SERIAL

#DEFINE LP  GPIO,0      ;LA SALIDA AL CONTROL DE LAMP -> RELAY
#DEFINE PIR GPIO,1      ;LA ENTRADA DEL SENSOR PIR              :1 = PRESENCIA, 0 = NADIE
#DEFINE LDR GPIO,2      ;LA ENTRADA DEL SENSOR DE LUZ -> LDR    :1 = LUZ        0 = POCA LUZ O SIN SUFICIENTE LUMINOSIDAD
#DEFINE SW  GPIO,3      ;LA ENTRADA DEL SENSOR SW INFRARROJO    :1 = PULSADO,   0 = SIN PULSAR
;#DEFINE RX  GPIO,4      ;RECIBO DE DATOS   -> FUTURO
;#DEFINE TX  GPIO,5      ;ENVIO DE DATOS    -> FUTURO
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;; INICIO DEL PROGRAMA
        ORG 0
        GOTO    INICIO

INICIO:
        MOVWF	OSCCAL	;CONFIGURO OSCILADOR INTERNO A 4Mhz
        MOVLW 	H'DF'   ;CARGO CONFIGURACION DEL OPTION
		OPTION			;ACTIVO COMO GP2 COMO I/O, TENDRA RESET INTERNO
		MOVLW	H'FE'	;CONFIGURAR ENTRADAS Y SALIDAS DEL uC
		TRIS	GPIO	;CONFIGURO EL PUERTO DE I/O
		CLRF	GPIO    ;LIMPIO EL PUERTO, APAGO LA LAMPARA

;;;;;;;;;;;;;; PROGRAMA PRINCIPAL ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
MAIN:
        BTFSC   PIR     ;PREGUNTO SI EL SENSOR PIR DECTECTO PRESENCIA
        GOTO    PIR_L
        BTFSC   SW      ;PREGUNTO SI FUE ACTIVADO EL SW
        GOTO    SW_L
        GOTO    MAIN    ;REGRESO A PREGUNTAR SI HA CAMBIADO ALGUNO
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
PIR_L:;PROCESAMIENTO DEL SENSOR PIR Y LDR
        BTFSC   SW      ;EN CASO DE QUE HAYA PRESENCIA Y SUFICIENTE LUZ, ENCEDERA LA LAMPARA
        GOTO    SW_L    ;ENCENDERA LA LAMPARA

        BTFSC   LDR     ;VERIFICO SI HAY POCA LUZ EN LA HABITACION
        GOTO    MAIN    ;SI HAY SUFICIENTE LUZ REGRESA A VERIFICAR EL STATUS DE LOS SENSORES
        BSF     LP      ;SI HAY POCA LUZ ENCIENDE LA LAMPARA
        GOTO    TIME0   ;TIEMPO DE ESPERA DE 5 MIN
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
SW_L:   CALL    REBOTE  ;TIEMPO DE ESPARA POR TRANSITORIO QUE SE PUEDA PRODUCIR
        BTFSC   SW      ;SI SIGUE TOCANDO EL BOTON TOUCH, REALIZA EL ENCENDIDO O APAGADO
        GOTO    SW_L    ;REGRESA AL MAIN A SEGUIR PREGUNTANDO

        BTFSC   LP       ;ESTA APAGADA LA LAMPARA?
        GOTO    APAGAR  ;APAGA LAMPARA SI VA PRESIONADO MAS DE UNA VEZ
        GOTO    ENCENDER;VA A ENCENDER LA LAMPARA

;;;;;;;;;;;;;;;;;;;;;;;;;; SENCCION DEL SW INFRARROJO ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
ENCENDER:
        BSF     LP      ;ENCIENDE LA LAMPARA
        GOTO    TIME1   ;COMIENZA EL CONTEO DE LOS 5 MIN

APAGA:; CUANDO ESTA EL TIEMPO PARA EL APAGADO DE LA LAMPARA PERO SE PRESIONA EL SW ANTES DE QUE TERMINE EL TIEMPO, LLEGA AQUI
        CALL    REBOTE
        BTFSC   SW
        GOTO    APAGA

APAGAR:; APAGADO NORMAL DEL SW DESPUES DE SUS 5 MINUTOS
        BTFSC   PIR     ;NO HAY PRESENCIA EN LA HABITACION?
        BTFSC   LDR     ;HAY SUFICIENTE LUZ?
        GOTO    OFF     ;APAGA LA LAMPARA
        GOTO    TIME0   ;COMO EXISTE PRESENCIA Y POCA LUZ,REINICIA EL TIEMPO DE ESPERA DE 5 MIN Y MANTIENE ENCENDIDA LA LAMPARA
OFF:    BCF     LP      ;APAGA LAMPARA
        GOTO    MAIN    ;REGRESA AL MAIN

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;SECCION DEL SENSOR PIR;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
OFF_PIR:
        BTFSC   PIR     ;NO HAY PRESENCIA, CONFIRMA QUE NO HAYA PRESENCIA EN EL CUARTO
        GOTO    PRE_PIR ;REINICIA LOS 5 MIN, VERIFICADNO LA LUZ DEL CUARTO
        BCF     LP      ;APAGA LAMPARA
        GOTO    MAIN    ;REGRESA A PREGUNTAR AL SW Y AL PIR

PRE_PIR:
        BTFSC   LDR     ;NO HAY SUFICIENTE ILUMINACION?                                           
        GOTO    OFF_LP  ;SI HAY SUFICIENTE LUZ APAGARA EL FOCO              
        GOTO    TIME0   ;CUENTA LOS CINCO MINUTOS DE NUEVO                 

OFF_LP:
        CALL    REBOTE  ;CUIDA EL REBOTE DE LA OSCILACION DE LA LUZ
        BTFSS   LDR     ;SI HAY LUZ BRINCA Y APAGA
        GOTO    TIME0   ;NO HAY LUZ, ENTONCES COMIENZA A CONTAR LOS 5 MIN
        BCF     LP      ;APAGA LAMPARA
        GOTO    MAIN    ;REGRESA AL PROGRAMA PRINCIPAL
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
        #INCLUDE "CINCO_MINUTOS.INC";RETARDOS DE CINCO MINUTOS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
        ORG 0X1C8
        DT  "SWITCH SMART V1.0"
        DT  "JOSEF COMPANY - IXO"
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
        END
